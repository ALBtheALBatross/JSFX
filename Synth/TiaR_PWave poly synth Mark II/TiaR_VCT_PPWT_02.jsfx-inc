//@noindex

desc:Voice Test rev02 library
/*
 Copyright 2014, Thierry Rochebois
 All rights reserved.

  Redistribution and use in source and binary forms, with or without 
  modification, are permitted provided that the following conditions are
  met:
  Redistributions of source code must retain the above copyright notice,
  this list of conditions and the following disclaimer. 

  Redistributions in binary form must reproduce the above copyright
  notice, this list of conditions and the following disclaimer in the
  documentation and/or other materials provided with the distribution. 

  The name of Thierry Rochebois may not be used to endorse or promote
  products derived from this software without specific prior written
  permission. 

  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS 
  "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
  LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
  A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT 
  OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
  SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT 
  LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
  DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
  THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT 
  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
  OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/
import TiaR_ADSR_03.jsfx-inc
import TiaR_LFO_04.jsfx-inc
@init
//_____________________________________________________________________
function VOX() (   
  VOXTL = map.l;
  VOXTLm1 = VOXTL - 1;  // & instead of %
  VOXTL128 = 128 * VOXTL;
  VOXTL_2pi = VOXTL/(2*$pi);

  VOX_COEF_TUNING = (VOXTL * TUNING / srate);
  
  VOX_tPitch = ad; ad += 129;
  i = 0;
  loop(129,
    VOX_tPitch[i] = VOX_COEF_TUNING * 2 ^ ( (i - 69)*(1/12) );
    i += 1;
  );
//_  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _
  ADSR();
);
//_____________________________________________________________________
function VOX_calcDP(pitch)
local(p0 a) (
  pitch = min(128, max(0, pitch));
  p0 = pitch|0;
  a = pitch - p0;
  VOX_tPitch[p0] + a * (VOX_tPitch[p0 + 1] - VOX_tPitch[p0]);
);
//_____________________________________________________________________
function VOX_init() (
  this.noteOn = 60;
  this.note = -1;
  this.activity = 0;
  this.dp = VOX_calcDP(this.note);
  this.p = 0;
  this.out = 0;
  this.env = 0;
  this.adsr.ADSR_init(0,1,0.2,1);
  this.adsrMod.ADSR_init(0,1,0.2,1);
  this.adsrFlt.ADSR_init(0,1,0.2,1);
  this.lfo1.LFO_init();
  this.lfo2.LFO_init();
);
//______________________________________________________________________
function VOX_procNoteOff()
local(i)(
  i = 0;
  loop(KB6_cpNoteOff,
    this.note === KB6_tNoteOff[i]   ?
      this.gate = 0;
    i += 1;
  );
);
//______________________________________________________________________
function VOX_kProc() (
  this.lfo1.LFO_kProc();
  this.lfo2.LFO_kProc();
//_  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _
  this.trig ? (
    this.note = this.noteOn;
    this.gate = this.noteVel*(1/127);
  );
//_  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _
  this.pitch = this.note + 12 * bend + 0.01 * slider29 * modWheel * this.lfo1.sin;
  this.dp = VOX_calcDP(this.pitch);
  this.dp2 = (1 + 0.01 * slider32) * this.dp;
  this.dp2 *= subOscRatio;
  
//_  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _
  this.new_env = this.adsr.ADSR_kProc(this.gate, this.trig);
  this.denv = (this.new_env - this.env) * ONE_OVER_KRATE;
  this.new_envMod = this.adsrMod.ADSR_kProc(this.gate, this.trig);
  this.new_envFlt = this.adsrFlt.ADSR_kProc(this.gate, this.trig);
  this.trig = 0;
  this.activity = this.new_env + this.adsr.AttackSeg;
  this.activity == 0 ? this.activity = 0;
//_  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _
  //Filter mode
  (slider15%5) === 0 ? this.mFlt = this.new_envFlt
: (slider15%5) === 1 ? this.mFlt = 0.5 * this.new_envFlt + 0.5 * modWheel
: (slider15%5) === 2 ? this.mFlt = 0.8 * this.new_envFlt + 0.1 + 0.1 * this.lfo2.sin
: (slider15%5) === 3 ? this.mFlt = 0.5 + 0.5 * this.lfo2.sin
: (slider15%5) === 4 ? this.mFlt = 0.5 * this.new_envFlt + 0.5 * aftertouch;
  this.mFlt = slider14 + this.mFlt * (slider13 - slider14);
  slider15 <= 4 ? this.mFlt = this.pitch - this.mFlt            //KF = 0
                : this.mFlt = max(this.pitch, 52 - this.mFlt);  //KF = 1
//_  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _
  //Modifier Mode              
//Env + LFOs,RND,Env + RND,Env x RND
  slider12 === 0 ? this.mMod = this.new_envMod
: slider12 === 1 ? this.mMod = 0.5 * this.new_envMod + 0.5 * modWheel
: slider12 === 2 ? this.mMod = 0.8 * this.new_envMod + 0.1 - 0.1 * this.lfo2.sin
: slider12 === 3 ? this.mMod = 0.5 - 0.5 * this.lfo2.sin
: slider12 === 4 ? this.mMod = 0.5 * this.new_envMod + 0.25 - 0.2 * this.lfo2.sin - 0.05 * this.lfo1.sin
: slider12 === 5 ? this.mMod = 0.5 - 0.5 * this.lfo2.rand
: slider12 === 6 ? this.mMod = 0.8 * this.new_envMod + 0.1 - 0.1 * this.lfo2.rand
: slider12 === 7 ? this.mMod = 0.8 * this.new_envMod * (0.5 - 0.5 * this.lfo2.rand)
: slider12 === 8 ? this.mMod = 0.5 * this.new_envMod + 0.5 * aftertouch;

  this.subGain = slider31;
  slider34 === 1 ? this.subGain *= aftertouch :
  slider34 === 2 ? this.subGain *= modWheel :
  slider34 === 3 ? this.subGain *= this.new_envMod :
  slider34 === 4 ? this.subGain *= this.new_envFlt ;
  
  map.MAP_kTriInterp(this, this.mMod * (map.nb-1), this.mFlt);
);
//_____________________________________________________________________
function VOX_aProc() (
  this.env += this.denv;
  this.p += this.dp;
  //this.p -= (this.p > VOXTL) * VOXTL;
  this.p > VOXTL ? this.p -= VOXTL;
  this.out = map.MAP_aTriInterp(this, this.p);
  this.out *= this.env;
);
//_____________________________________________________________________
function VOX_aProcChorus() (
  this.env += this.denv;
  this.p += this.dp * gDPCOEF;
  this.p > VOXTL ? this.p -= VOXTL;
  this.out = map.MAP_aTriInterp(this, this.p);
//_  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _
  this.p2 += this.dp2;
  this.p2 > VOXTL ? this.p2 -= VOXTL;
  this.out += this.subGain * map.MAP_aTriInterp(this, this.p2);
//_  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _    
  this.out *= 0.5 * this.env;
);
function VOX_gActivity(a x y w h) local(m)(
  gfx_r = 1; gfx_g = 1; gfx_b = 1;
  gfx_rect(x,y,w,h);
  gfx_r = 0; gfx_g = 0; gfx_b = 0;
  gfx_rect(x+1,y+1,w-2,h-2);

  a = a^0.5;
  gfx_r = min(1, 2*a);
  gfx_g =  (a^0.7) - (a^1.5);
  gfx_b = 0;
  gfx_rect(x+2,y+2,w-4,h-4);

);
function VOX_gActivities(x y w h)
local(h_6)(
  h_6 = h * (1/6);
  VOX_gActivity(v1.activity, x, y,           w, h_6);
  VOX_gActivity(v2.activity, x, y + 1 * h_6, w, h_6);
  VOX_gActivity(v3.activity, x, y + 2 * h_6, w, h_6);
  VOX_gActivity(v4.activity, x, y + 3 * h_6, w, h_6);
  VOX_gActivity(v5.activity, x, y + 4 * h_6, w, h_6);
  VOX_gActivity(v6.activity, x, y + 5 * h_6, w, h_6);
);
